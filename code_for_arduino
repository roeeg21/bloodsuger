#include <ESP8266WiFi.h>
#include <WiFiClientSecure.h>
#include <ArduinoJson.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

// Wi-Fi credentials
const char* ssid = "";
const char* password = "";

// OLED setup
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET    -1
#define OLED_ADDRESS  0x3C
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

// LED pins
const int RED_LED = D8;
const int YELLOW_LED = D7;
const int GREEN_LED = D6;

// API info
const char* host = "bloodsuger.onrender.com";
const int httpsPort = 443;

// Bitmaps
static const unsigned char PROGMEM arrow_down_bits[] = {0x1f,0xe0,0x10,0x20,0x17,0xa0,0x16,0xa0,0x15,0xa0,0x16,0xa0,0x15,0xa0,0xf6,0xbc,0x85,0x84,0x5f,0xe8,0x2f,0xd0,0x17,0xa0,0x0b,0x40,0x04,0x80,0x03,0x00};

static const unsigned char PROGMEM arrow_up_bits[] = {0x03,0x00,0x04,0x80,0x0b,0x40,0x17,0xa0,0x2f,0xd0,0x5f,0xe8,0x86,0x84,0xf5,0xbc,0x16,0xa0,0x15,0xa0,0x16,0xa0,0x15,0xa0,0x17,0xa0,0x10,0x20,0x1f,0xe0};

static const unsigned char PROGMEM arrow_right_bits[] = {0x00,0x60,0x00,0x00,0x50,0x00,0x00,0x48,0x00,0x00,0x44,0x00,0x7f,0xd2,0x00,0x80,0x19,0x00,0xbf,0xfc,0x80,0xbf,0xfe,0x40,0xbf,0xfc,0x80,0x80,0x19,0x00,0x7f,0xd2,0x00,0x00,0x44,0x00,0x00,0x48,0x00,0x00,0x50,0x00,0x00,0x60,0x00};


void setup() {
  Serial.begin(115200);
  delay(100);

  // LED setup
  pinMode(RED_LED, OUTPUT);
  pinMode(YELLOW_LED, OUTPUT);
  pinMode(GREEN_LED, OUTPUT);
  digitalWrite(RED_LED, LOW);
  digitalWrite(YELLOW_LED, LOW);
  digitalWrite(GREEN_LED, LOW);

  // OLED init
  if (!display.begin(SSD1306_SWITCHCAPVCC, OLED_ADDRESS)) {
    Serial.println(F("SSD1306 allocation failed"));
    for (;;);
  }

  display.clearDisplay();
  display.setTextSize(1);
  display.setTextColor(SSD1306_WHITE);

  WiFi.begin(ssid, password);
  display.setCursor(0, 0);
  display.println("Connecting WiFi...");
  display.display();

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("\nWiFi connected");
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println("WiFi connected");
  display.display();
  delay(1000);
}

void loop() {
  WiFiClientSecure client;
  client.setInsecure();

  if (!client.connect(host, httpsPort)) {
    Serial.println("Connection failed");
    return;
  }

  client.print(String("GET ") + "/" + " HTTP/1.1\r\n" +
               "Host: " + host + "\r\n" +
               "Connection: close\r\n\r\n");

  while (client.connected()) {
    String line = client.readStringUntil('\n');
    if (line == "\r") break;
  }

  String payload = client.readString();
  Serial.println(payload);

  StaticJsonDocument<300> doc;
  DeserializationError error = deserializeJson(doc, payload);
  if (error) {
    Serial.println("JSON parse failed");
    return;
  }

  int glucose = doc["Glucose"];
  const char* trend = doc["Trend"];

  // -------- OLED Display --------
  display.clearDisplay();


display.drawCircle(47, 30, 30, 1);

display.setTextColor(1);
display.setTextSize(3);
display.setTextWrap(false);
display.setCursor(23, 19);
display.print(glucose);

display.setTextSize(1);
display.setCursor(33, 45);
display.print("mg/dl");


  // Draw arrow for trend
  const uint8_t arrowX = 90;
  const uint8_t arrowY = 28;
  if (strcmp(trend, "rising") == 0) {
    display.drawBitmap(arrowX, arrowY, arrow_up_bits, 36, 30, 1);
  } else if (strcmp(trend, "falling") == 0) {
    display.drawBitmap(arrowX, arrowY, arrow_down_bits, 36, 30, 1);
  } else {
    display.drawBitmap(arrowX, arrowY, arrow_right_bits, 36, 30, 1); // steady or unknown
  }

  display.display();

  // -------- LED Logic --------
  digitalWrite(RED_LED, LOW);
  digitalWrite(YELLOW_LED, LOW);
  digitalWrite(GREEN_LED, LOW);

  if (glucose < 60) {
    digitalWrite(RED_LED, HIGH);
  } else if (glucose > 200) {
    digitalWrite(YELLOW_LED, HIGH);
  } else {
    digitalWrite(GREEN_LED, HIGH);
  }

  delay(10000); // Update every 10 sec
}
